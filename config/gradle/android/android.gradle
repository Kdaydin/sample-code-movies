apply from: '../config/gradle/lang/java.gradle'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

ext.app = [

        major: versionMajor,
        minor: versionMinor,
        patch: versionPatch,
        build: versionBuild,

]

ext.sdk = [

        min_sdk    : 21,
        target_sdk : 28,
        compile_sdk: 28,

]

ext.suffix = [

        debug: 'debug',

]

android {
    compileSdkVersion sdk.compile_sdk
    defaultConfig {
        minSdkVersion sdk.min_sdk
        targetSdkVersion sdk.target_sdk
    }
    compileOptions {
        sourceCompatibility java.version
        targetCompatibility java.version
    }
    buildTypes {
        def themoviedbApiPropertiesFile = file(keys.themoviedb_api_properties_file_path)
        def themoviedbApiProperties = new Properties()
        themoviedbApiProperties.load(new FileInputStream(themoviedbApiPropertiesFile))
        def themoviedb_api_key = themoviedbApiProperties['themoviedb_api_key']

        debug {
            buildConfigField 'String', 'THEMOVIEDB_API_KEY', "\"${themoviedb_api_key}\""
        }
        release {
            buildConfigField 'String', 'THEMOVIEDB_API_KEY', "\"${themoviedb_api_key}\""
        }
    }
    testOptions {
        unitTests {
            all {
                testLogging { // Print test details in the terminal (or CI).
                    events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                    exceptionFormat 'full'
                }
            }
        }
    }
    sourceSets {
        main {
            java.srcDirs = [
                    'src/main/kotlin',
            ]
        }
        test {
            java.srcDirs = [
                    'src/test/kotlin',
            ]
            resources.srcDirs = [
                    'src/test/resources',
            ]
        }
        androidTest {
            java.srcDirs = [
                    'src/androidTest/kotlin',
            ]
        }
    }
}
